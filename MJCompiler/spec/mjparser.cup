package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;

import rs.ac.bg.etf.pp1.ast.*;


parser code {:
	
	boolean errorDetected = false;
	
	Logger log = Logger.getLogger(getClass());
   
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }
    
    public void report_info(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.info(msg.toString());
    }
    
:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}


terminal PROG;
terminal ASSIGN;
terminal LBRACKET, RBRACKET, LBRACE, RBRACE, LPAREN, RPAREN;
terminal SEMI, COMMA, DOT, COLON, ARROW;
terminal PRINT, READ, NEW; 
terminal VOID;
terminal WHILE, FOREACH, IF, ELSE, CONTINUE, BREAK, RETURN;
terminal CONST;

terminal Integer NUMBER;
terminal Character CHAR;
terminal Boolean BOOL;
terminal String IDENT;

terminal PLUS, MINUS;
terminal INC, DEC;	 	
terminal MUL, DIV, MOD;				
terminal EQ, NE, GT, GE, LT, LE;
terminal OR, AND;

terminal CLASS, EXT;

nonterminal ProgDeclList, OptionalMethodDeclList, MethodDeclList;
nonterminal ConstDeclStart, ConstList, ConstDeclaration;
nonterminal ProgVarDeclStart, ProgVarType, ProgVarList, ProgVarDeclaration;

nonterminal ProgClassDecl, OptionalClassExtends, OptionalClassMethodDecl, SuperClass;
nonterminal ConstructorDeclList, ConstructorDecl, ClassNewType;

nonterminal VarDeclStart, VarDeclList, VarList;
nonterminal FieldDeclStart, FieldDeclList, FieldList;
nonterminal FormalParamList, FormalParamDecl, FormalParams; 
nonterminal OptionalStatementList, StatementList, Statement;
nonterminal IfConstruct, WhileConstruct, ControlCondition, IfKeyWord, WhileKeyWord, ElseKeyWord;
nonterminal OrKeyWord, AndKeyWord, ForeachKeyWord;
nonterminal ForeachConstruct, ForeachDesignator;
nonterminal DesignatorStatement;
nonterminal DesignatorListStart, DesignatorListContent, DesignatorDummy, DesignatorListStartDummy;
nonterminal FunctionName, OptionalMethodActualParams, ActualParameter, ActualParamList;

nonterminal Relop, Addop, Mulop; 	

nonterminal rs.etf.pp1.symboltable.concepts.Obj Program, ProgName, Designator, ForIdent, ConstType;
nonterminal rs.etf.pp1.symboltable.concepts.Obj ClassName, ConstructorDeclStart, MethodDeclaration, MethodTypeAndName; 
nonterminal rs.etf.pp1.symboltable.concepts.Struct Type, Term, Expr, AssignmentStatementExpr, Factor, VarName;
nonterminal rs.etf.pp1.symboltable.concepts.Struct FunctionCall, Condition, ConditionTerm, ConditionFactor; 

/* solves the IF condition ambiguity */
precedence left ELSE;


Program ::= (Program) PROG ProgName:p ProgDeclList LBRACE OptionalMethodDeclList RBRACE 
;

ProgName ::= (ProgName) IDENT:pName 
;



ProgDeclList ::= (ProgListConst) ProgDeclList ConstDeclStart 
				 |
				 (ProgListVar) ProgDeclList ProgVarDeclStart
				 |
				 (ProgListClass) ProgDeclList ProgClassDecl
				 |
				 (NoProgDeclList) /* epsilon */
				 ;


ConstDeclStart ::= (ConstDeclarationStart) CONST Type:constType ConstList SEMI
;			



ConstList ::= (ConstDeclarationList) ConstList COMMA ConstDeclaration
			  |
			  (BaseConstDeclarationList) ConstDeclaration
			  ;			  
			  
ConstDeclaration ::= (ConstDeclaration) IDENT:constName ASSIGN ConstType
;

ConstType ::= (ConstIntType) NUMBER
			  |
			  (ConstCharType) CHAR
			  |
			  (ConstBoolType) BOOL
			  ;
			   
			   
VarDeclStart ::= (VarDeclarationStart) Type VarList SEMI
;


VarList ::= (VarDeclarationList) VarName COMMA VarList
		    |
		    (BaseVarDeclarationList) VarName
		    |
			(VarDeclarationCommaError) error COMMA:l
			{: parser.report_error("Oporavak od greske do , na liniji " + lleft, null); :}
			VarList
			|
			(VarDeclarationSemiError) error:l
			{: parser.report_error("Oporavak od greske do ; na liniji " + lleft, null); :}
		    ;
		    
VarName ::= (VarNameSingle) IDENT:varName
			|
			(VarNameArray) IDENT:varName LBRACKET RBRACKET
			;

		     
VarDeclList ::= (VarDeclarations) VarDeclStart VarDeclList 
				|
				(NoVarDecl)/* epsilon */
				;		

			   
			   
ProgVarDeclStart ::= (ProgVarDeclarationStart) ProgVarType ProgVarList SEMI 
;



ProgVarType ::= (ProgVarType) Type
;


ProgVarList ::= (ProgVarListRec) ProgVarDeclaration COMMA ProgVarList
				|
				(BaseProgVarDeclaration) ProgVarDeclaration
				|
				(ProgVarDeclarationCommaError) error COMMA:l
				{: parser.report_error("Oporavak od greske do , na liniji " + lleft, null); :}
				ProgVarList
				|
				(ProgVarListSemiError) error:l
				{: parser.report_error("Oporavak od greske do ; na liniji " + lleft, null); :}
				;
				
ProgVarDeclaration ::= (ProgVar) VarName
;


ProgClassDecl ::= (ClassDeclaration) ClassName OptionalClassExtends FieldDeclList OptionalClassMethodDecl RBRACE
;


ClassName ::= (ClassName) CLASS IDENT:name
;

OptionalClassExtends ::= (ClassExtends) EXT SuperClass LBRACE
						 |
						 (NoClassExtends) /* epsilon */ LBRACE
						 |
						 (ExtendsError) error LBRACE:l
						 {: parser.report_error("Oporavak od greske do { u liniji " + lleft, null); :}
						 ;

SuperClass ::= (SuperClassIdent) IDENT:name
;


FieldDeclList ::= (FieldDeclListExists) FieldDeclStart FieldDeclList
				  |
				  (NoFieldDecl) /* epsilon */
				  ;
				  
FieldDeclStart ::= (FieldDeclarationStart) Type FieldList SEMI
				   |
				   (FieldDeclErrorSemi) error SEMI:l
				   {: parser.report_error("Oporavak od greske do ; na liniji " + lleft, null); :}
				   |
				   (FieldDeclErrorBrace) error:l
				   {: parser.report_error("Oporavak od greske do { na liniji " + lleft, null); :}
				   ;
				   				  
FieldList ::= (FieldDeclarationList) VarName COMMA FieldList
		      |
		      (BaseFieldDeclarationList) VarName
		      ;
						 
OptionalClassMethodDecl ::= (ClassMethodDecl) LBRACE ConstructorDeclList OptionalMethodDeclList RBRACE
							|
							(NoClassMethodDecl) /* epsilon */
							;


ConstructorDeclList ::= (ConstructorDeclarationList) ConstructorDeclList ConstructorDecl
						|
						(ConstructorDeclarationEmpty) /* epsilon */
						;
						
ConstructorDecl ::= (ConstructorDeclarationInst) ConstructorDeclStart FormalParams RPAREN VarDeclList LBRACE OptionalStatementList RBRACE
;

					
ConstructorDeclStart ::= (ConstructorDeclarationStart) IDENT LPAREN
;


/* Method declaration (definition) grammar */
OptionalMethodDeclList ::= (MethodDeclListExists) MethodDeclList
							|
							(NoMethodDeclList) /* epsilon */
							;


MethodDeclList ::= (MethodDeclarations) MethodDeclList MethodDeclaration
				   |
				   (MethodDeclSingle) MethodDeclaration
				   ;

MethodDeclaration ::= (MethodDecl) MethodTypeAndName LPAREN FormalParams RPAREN VarDeclList LBRACE OptionalStatementList RBRACE 
					  |
					  (MethodDeclError) MethodTypeAndName LPAREN error RPAREN:l VarDeclList LBRACE OptionalStatementList RBRACE
					  {: parser.report_error("Oporavak od greske do ) na liniji " + lleft, null); :}
					  ;

MethodTypeAndName ::= (MethodTypeName) Type:retType IDENT:methName
					  |
					  (MethodVoidName) VOID IDENT:methName
					  ;
				   

FormalParams ::= (OptFormalParams) FormalParamList
			   	 |
			     (NoFormalParam) /* epsilon */
			     ;

FormalParamList ::= (FormalParamDecls) FormalParamDecl COMMA FormalParamList
					|
					(SingleFormalParamDecl) FormalParamDecl
					|
					(FormalParamListError) error COMMA:l
					{: parser.report_error("Oporavak od greske do , na liniji " + lleft, null); :}
				    FormalParamList
					;
					
FormalParamDecl ::= (FormalParamDeclSingle) Type:paramType IDENT:name
					|
					(FormalParamDeclArray) Type:paramType IDENT:name LBRACKET RBRACKET
					;


/* Statement grammar */
OptionalStatementList ::= (OptStatementList) StatementList
						  |
						  (NoStatementList) /* epsilon */
						  ;


StatementList ::= (Statements) StatementList Statement 
				  |
				  (StatementSingle) Statement
				  ;

Statement ::= (DesignatorStatementStmt) DesignatorStatement SEMI
			  |
			  (WhileStatement) WhileConstruct
			  |
			  (ForeachStatement) ForeachConstruct
			  |
			  (PrintStatement) PRINT LPAREN Expr RPAREN SEMI
			  |
			  (PrintWidthStatement) PRINT LPAREN Expr COMMA NUMBER RPAREN SEMI
			  |
			  (ReadStatement) READ LPAREN Designator RPAREN SEMI
			  |
			  (BreakStatement) BREAK SEMI
			  |
			  (ContinueStatement) CONTINUE SEMI
			  |
			  (ReturnStatement) RETURN SEMI
			  |
			  (ReturnExprStatement) RETURN Expr SEMI
			  |
			  (IfStatement) IfConstruct
			  |
			  (IfElseStatement) IfConstruct ElseKeyWord Statement
			  |
			  (BlockStatement) LBRACE OptionalStatementList RBRACE
			  |
			  (StatementError) error SEMI:l
			  {: parser.report_error("Oporavak od greske do ; na liniji " + lleft, null); :} 
			  ;

			  
IfConstruct ::= (IfConstruct) IfKeyWord ControlCondition Statement
;			  

IfKeyWord ::= (IfKeyWord) IF
;

ElseKeyWord ::= (ElseKeyWord) ELSE
;

WhileConstruct ::= (WhileConstruct) WhileKeyWord ControlCondition Statement
;

WhileKeyWord ::= (WhileKeyWord) WHILE
;

ForeachConstruct ::= (ForeachConstruct) ForeachDesignator ForeachKeyWord LPAREN ForIdent ARROW Statement RPAREN SEMI
;

ForeachKeyWord ::= (ForeachKeyWord) FOREACH
;

ForIdent ::= (ForIdent) IDENT:name
;

ForeachDesignator ::= (ForeachDesignator) Designator DOT
;


ControlCondition ::= (ControlConditionValid) LPAREN Condition RPAREN
					 |
					 (ControlConditionError) LPAREN error:l RPAREN
					 {: parser.report_error("Oporavak od greske do ) na liniji " + lleft, null); :}
					 ;



/* Condition hierarchy (AND > OR) */
Condition ::= (ConditionOr) Condition OrKeyWord ConditionTerm
			  |
			  (ConditionSingle) ConditionTerm
			  ;
			  
ConditionTerm ::= (ConditionTermAnd) ConditionTerm AndKeyWord ConditionFactor
				  |
				  (ConditionTermSingle) ConditionFactor
				  ;
				  
ConditionFactor ::= (ConditionFactorRel) Expr Relop Expr
					|
					(ConditionFactorSingle) Expr
					;

OrKeyWord ::= (OrKeyWord) OR
;

AndKeyWord ::= (AndKeyWord) AND
;

		   

/* Designator */
DesignatorStatement ::= (AssignmentStatement) AssignmentStatementExpr
						|
						(MultipleAssignmentStatement) DesignatorListStart Designator
						|
						(DesignatorStmtFunctionCall) FunctionCall
						|
						(IncrementDesignator) Designator INC
						|
						(DecrementDesignator) Designator DEC
						;
						


AssignmentStatementExpr ::= (AssignmentExpr) Designator ASSIGN Expr
;
					   	

					   	
DesignatorListStart ::= (DesignatorListStart) DesignatorListStartDummy LBRACKET DesignatorListContent RBRACKET ASSIGN
;

DesignatorListStartDummy ::= (DesignatorListStartDummy) /* epsilon */
; 

DesignatorListContent ::= (DesignatorListCommaElem) DesignatorDummy COMMA DesignatorListContent
						  |
						  (DesignatorListDummy) DesignatorDummy
						  ;
			
DesignatorDummy ::= (DesignatorDummy) /* epsilon */
					|
					(DesignatorReal) Designator
					;			
					   	   	


Designator ::= (DesignatorSingle) IDENT:name
			   |
			   (DesignatorArray) Designator LBRACKET Expr RBRACKET
			   |
			   (DesignatorField) Designator DOT IDENT:rightField
			   ;



FunctionCall ::= (FunctionCall) FunctionName OptionalMethodActualParams RPAREN
;

FunctionName ::= (FunctionName) Designator LPAREN
;

OptionalMethodActualParams ::= (OptMethodActualParams) ActualParamList
							   |
							   (NoMethodActualParams) /* epsilon */
							   ;

ActualParamList ::= (ActualParamListRec) ActualParamList COMMA ActualParameter
					|
					(ActualParamSingle) ActualParameter
					;							   							   

ActualParameter ::= (ActualParameter) Expr:exp
;


Type ::= (Type) IDENT:typeName 
;


/* Grammar for operands, based on priority (Mulop > Addop) */
Expr ::= (NegExpr) MINUS Term
		 |
		 (AddExpr) Expr:typeExpr Addop Term:t
		 |
		 (TermExpr) Term:t
		 ;

Term ::= (MulTerm) Term Mulop Factor:t
		 | 
		 (FactorTerm) Factor:t 
		 ;

Factor ::= (ConstFactor) ConstType
		   |
		   (DesignatorFactor) Designator:d
		   |
		   (OperatorNewClass) ClassNewType LPAREN OptionalMethodActualParams RPAREN
		   |
		   (OperatorNewArr) NEW Type LBRACKET Expr RBRACKET
		   |
		   (FactorFuncCall) FunctionCall
		   |
		   (ExprFactor) LPAREN Expr RPAREN
		   ;
		   
ClassNewType ::= (ClassNewType) NEW Type
;

Addop ::= (PlusOp) PLUS 
		  |
		  (MinusOp) MINUS
		  ;
		  
Relop ::= (EqualsOp) EQ
		  |
		  (NotEqualsOp) NE
		  |
		  (GreaterOp) GT
		  |
		  (LessOp) LT
		  |
		  (GreaterEqualOp) GE
		  |
		  (LessEqualOp) LE
		  ;
		  
Mulop ::= (MultiplyOp) MUL
		  |
		  (DivisionOp) DIV
		  |
		  (ModuloOp) MOD
		  ;
